# Maintainer: Benoit Pierre <benoit.pierre@gmail.com>

pkgname=plover
pkgdesc="Free and open source real-time stenography engine."
pkgver=4.0.0.dev1
pkgrel=1
arch=('any')
license=('GPL2')
depends=(
  'python'
  'python-appdirs'
  'python-dbus'
  'python-pyqt5'
  'python-pyserial'
  'python-setuptools'
  'python-xlib'
)
makedepends=(
  'python-babel'
  'python-mock'
  'python-pytest'
  'python-setuptools-scm'
)
provides=('plover')
conflicts=('plover-git')
url="http://www.openstenoproject.org/plover/"
source=(https://github.com/openstenoproject/plover/archive/v$pkgver.tar.gz)
sha1sums=(803bb68068feef3772e34e1b1ab34e7fa25375ae)

prepare() {
  cd "$pkgname-$pkgver"
  sed -i '/^\s*PyQt5\b.*/d' setup.cfg
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -vDm644 -t "${pkgdir}/usr/share/pixmaps" plover/assets/plover.png
  install -vDm644 -t "${pkgdir}/usr/share/applications" application/plover.desktop
  chmod og+rX -R "$pkgdir"
}

# vim:set sw=2 sts=2 et:
